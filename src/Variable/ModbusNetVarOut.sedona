@niagaraIcon="module://icons/x16/control/numericPoint.png"
class ModbusNetVarOut
  extends FieldBusIO
{
  @readonly
  property float out = 0.0f

  property float in = 0.0f

  @summary=true
  @config
  @asStr
  property Buf(64) alias = ""

  @config
  property bool isOutofService = false
  
  @config
  property float outofservice_value = 0.0f
  
  override void changed(Slot slot)
  {
    super.changed(slot)
    if (slot == ModbusNetVarOut.in) {
      if (cache != 0 && bus != null && status <= STATUSOK) {
        buf[0] = in
        bus.writeReg(cache, regId, typeId, buf)
      }
    } 
    if (slot == ModbusNetVarOut.device_name || slot == ModbusNetVarOut.io_type || slot == ModbusNetVarOut.Address) {
      update()
      needUpdate = true
    }
  }
  virtual override void execute()
  {
	if(isOutofService == true)
	{
		out := outofservice_value
        log.message("ModbusNetVarOut:: isOutofService == true")  // status=4 -> STATUSNODEV
        return
	}
    if (cache != 0 && bus != null && status <= STATUSREADY) {
      if (bus.readReg(cache, regId, typeId, buf) == dataLen) {
        out := buf[0]
        status := STATUSOK
      } else {
        out := 0.0f
        status := STATUSOFFLINE
      }
    }
    if (needUpdate && isSlotLinkToIN() && cache != 0 && bus != null && status <= STATUSOK) {
      buf[0] = in
      bus.writeReg(cache, regId, typeId, buf)
      needUpdate = false
    }
    if (!isSlotLinkToIN() || cache == 0) {
      needUpdate = true
    }
  }
  private bool isSlotLinkToIN()
  {
    for (Link link = linksTo; link != null; link = link.nextTo) {
      Slot slot = type.slot(link.toSlot)
      if (slot == ModbusNetVarOut.in) {
        return true
      }
    }
    return false
  }

  private inline float[1] buf
  private bool needUpdate = true
}
