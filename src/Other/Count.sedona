//
// Copyright (c) 2010-2011 Tridium, Inc
// Licensed under the Academic Free License version 3.0
//
// History:
//   12 Feb 2010  Clif Turman    Creation
//   04 Aug 2011  Clif Turman    Add comments
//   16 Aug 2011  Clif Turman    Fix issue 20487: Default should be to count up
//   17 Aug 2011  Clif Turman    Added @min=0 facet to preset property
//

**
** pulse counter object, counts transitions form 0 to 1 of input "in"
** Counts up if dir == true, counts down if dir == false
** Forced to preset value if enable = false
** In the case where dir == false, the counter will stop counting down at 0
**
@niagaraIcon="module://icons/x16/control/util/step.png"
class Count
  extends Component
{
  **
  ** Number of times "in" property has transitioned from 0 to 1
  **
  @readonly property float out
  
  **
  ** input, transitions from false to true increment out by 1
  **
  property float in

  **
  ** Presets the counter to a specific value, defaults to 0
  **
  @defaultOnClone
  @min = 0
  @config property float preset = 0.0
	
  **
  ** Configures direction.  True = "up", False = "down"
  **
  @trueText = "up"
  @falseText = "down"
  @defaultOnClone
  @config property float dir = 1.0

  **
  ** enable input
  **
  property float en = 1.0

  **
  ** if reset is true, out = preset and no counting takes place
  **
  property float reset

  **
  ** Override of start method to initialize the counter
  **
  virtual override void start()
  {
    out := preset
    lastIn = in
  }

  **
  ** Execute is called once every scan.
  ** increment/decrement the count on rising edge of "in" if "enable" is true 
  **
  virtual override void execute()
  {
    if(en == 0.0) return
  	
    float tcnt = out

    if((lastIn == 0.0) & (in != 0.0))
    {
      if(dir != 0.0) tcnt++
      else    tcnt--
      if (tcnt < 0.0) tcnt = 0.0
    }
    if(reset != 0.0) tcnt = preset

    out := tcnt

    lastIn = in
  }
  
  private float lastIn

}

